PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sapo: <http://www.yso.fi/onto/sapo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX suo: <http://www.yso.fi/onto/suo/>
PREFIX sipa: <http://ldf.fi/siso/sipa/>
PREFIX sische: <http://ldf.fi/siso/schema/>
BASE <http://ldf.fi/siso/sipa/>

CONSTRUCT {
  
  ?Suomi a sische:Place ;
    skos:prefLabel "Suomi"@fi ;
    skos:exactMatch <http://www.yso.fi/onto/sapo/Suomi(1917-1920)> ;
    ecrm:P2_has_type sische:Country .
  
  ?countyURI a sische:Place ;
    skos:prefLabel ?countyLabel ;
    skos:exactMatch ?county ;
    ecrm:P2_has_type sische:Province ;
    skos:broder ?Suomi .
      
  ?placeURI a sische:Place ;
    skos:prefLabel ?placeLabel ;
    skos:exactMatch ?place ;
    skos:broader ?countyURI ;
    ecrm:P2_has_type sische:Municipality .
    
}
  
WHERE {
  ?place a suo:kunta .
  ?place sapo:begindate ?startDate .
  OPTIONAL {
  	?place sapo:enddate ?endDate .
  }
  FILTER (?startDate <= '1918-01-01'^^xsd:date) .
  FILTER (?endDate >= '1918-01-01'^^xsd:date || !bound(?endDate))
  ?place sapo:placename ?placeName .
  
  OPTIONAL {
    ?place sapo:partof ?county .
    ?county sapo:begin ?countyBegin .
    OPTIONAL {
      ?county sapo:end ?countyEnd .
    }
    FILTER (?countyBegin <= "1918")
    FILTER (?countyEnd >= "1918" || !bound(?countyEnd))
    ?county sapo:placename ?countyName .
    
    BIND (STRAFTER(STR(?county), 'http://www.yso.fi/onto/sapo/') AS ?countyCode) .
    BIND (URI(STRBEFORE(STR(?countyCode), '(')) AS ?countyURI) .
    BIND (STRLANG(?countyName, 'fi') AS ?countyLabel) .
  }
  
  BIND (STRAFTER(STR(?place), 'http://www.yso.fi/onto/sapo/') AS ?sapoCode)
  BIND (STRBEFORE(STR(?sapoCode), "(") AS ?code)
  
  BIND (
    IF(STR(?code) = 'Salo', 'Salo_Ol',
    IF(STR(?code) = 'Salo_2', 'Salo_Tl',
    IF(STR(?code) = 'Ulvila_2', 'Ulvila',
    IF(STR(?code) = 'Degerby_2', 'Degerby',
            STR(?code)))))
              AS ?placeCode)
  
  BIND (
    IF(STR(?placeName) = 'Salo', 'Salo (Ol.)',
    IF(STR(?placeName) = 'Salo_2', 'Salo (Tl.)',
    IF(STR(?placeName) = 'Ulvila_2', 'Ulvila',
    IF(STR(?placeName) = 'Degerby_2', 'Degerby',
            STR(?placeName)))))
              AS ?placeCorrectedName)
  
  BIND (URI(STR(?placeCode)) AS ?placeURI) .
  BIND (STRLANG(?placeCorrectedName, 'fi') AS ?placeLabel) .
  
  BIND (URI('Suomi') AS ?Suomi) .
}

